plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.naman14.androidlame'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                
                groupId = 'com.github.kaushik-naik'
                artifactId = 'TAndroidLame'
                version = '1.2'
                
                pom {
                    name = 'TAndroidLame'
                    description = 'A powerful and easy-to-use Android wrapper around LAME MP3 encoder'
                    url = 'https://github.com/kaushik-naik/TAndroidLame'
                    
                    licenses {
                        license {
                            name = 'GNU General Public License v3.0'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                        }
                    }
                    
                    developers {
                        developer {
                            id = 'kaushik-naik'
                            name = 'Kaushik Naik'
                            email = 'kaushiknaik@gmail.com'
                        }
                    }
                    
                    scm {
                        connection = 'scm:git:github.com/kaushik-naik/TAndroidLame.git'
                        developerConnection = 'scm:git:ssh://github.com/kaushik-naik/TAndroidLame.git'
                        url = 'https://github.com/kaushik-naik/TAndroidLame/tree/main'
                    }
                }
            }
        }
    }
}